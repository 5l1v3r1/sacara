%{

open System
open ES.Sacara.Ir.Parser.IrAst
open ES.Sacara.Ir.Parser.IrParserUtility

%}

%start program
%token PROC ENDP PUSH POP ADD NOP CALL NCALL WRITE NWRITE READ NREAD 
%token GETIP RET JUMP JUMPIFL JUMPIFLE JUMPIFG JUMPIFGE ALLOCA HALT 
%token CMP GETSP SWRITE SREAD BYTE WORD DWORD NEWLINE COMMA SUB MUL 
%token DIV MOD AND SHIFTR SHIFTL OR NOT XOR NOR SETIP SETSP INC EOF
%token LPAREN RPAREN JUMP_DIRECTIVE MOV_DIRECTIVE PLUS_OP MINUS_OP MUL_OP DIV_OP MOD_OP
%token ADD_DIRECTIVE SUB_DIRECTIVE MUL_DIRECTIVE DIV_DIRECTIVE MOD_DIRECTIVE CMP_DIRECTIVE AND_DIRECTIVE OR_DIRECTIVE 
%token READB_DIRECTIVE READW_DIRECTIVE READDW_DIRECTIVE
%token WRITEB_DIRECTIVE WRITEW_DIRECTIVE WRITEDW_DIRECTIVE
%token NREADB_DIRECTIVE NREADW_DIRECTIVE NREADDW_DIRECTIVE
%token NWRITEB_DIRECTIVE NWRITEW_DIRECTIVE NWRITEDW_DIRECTIVE
%token SHIFTR_DIRECTIVE SHIFTL_DIRECTIVE XOR_DIRECTIVE NOR_DIRECTIVE INC_DIRECTIVE 
%token NCALL_DIRECTIVE INCLUDE

%token <string> INVOKE_DIRECTIVE
%token <string> STRING
%token <string> IDENTIFIER
%token <string> LABEL
%token <int32> INTEGER

%left PLUS_OP MINUS_OP MUL_OP DIV_OP MOD_OP

%type < Program > program

%%

program:
    | sourceElementList EOF																	{ Program (List.rev $1) }

sourceElementList:
	| procDefinition																		{ [$1] }
	| sourceElementList procDefinition														{ $2::$1 }
	| globalStatement																		{ [$1] }
	| sourceElementList globalStatement														{ $2::$1 }

globalStatement:
	| INCLUDE STRING																		{ includeFile($2) }

procDefinition:
	| PROC IDENTIFIER statementList ENDP													{ procedureDefinition($2, List.rev $3) }
	| PROC IDENTIFIER LPAREN parameterList RPAREN statementList ENDP						{ procedureDefinitionWithArguments($2, List.rev $4, List.rev $6) }

parameterList:
	|																						{ List.empty }
	| IDENTIFIER																			{ [$1] } 
	| parameterList COMMA IDENTIFIER														{ $3::$1 }

statementList:
	| statement																				{ [$1] }
	| statementList statement																{ $2::$1 }

statement:
	| LABEL statementNoLabel																{ label($1, $2) }
	| statementNoLabel																		{ $1 }

statementNoLabel:
	| RET																					{ ret() }
	| NOP																					{ nop() }
	| ADD																					{ add() }
	| PUSH primaryExpression																{ push($2) }
	| POP IDENTIFIER																		{ pop($2) }	
	| INC																					{ inc() }	
	| JUMP																					{ jump() }
	| JUMPIFL																				{ jumpIf(false, true) }
	| JUMPIFLE																				{ jumpIf(true, true) }
	| JUMPIFG																				{ jumpIf(false, false) }
	| JUMPIFGE																				{ jumpIf(true, false) }	
	| READ																					{ read() }
	| NREAD																					{ readNative() }
	| WRITE																					{ write() }
	| NWRITE																				{ writeNative() }
	| ALLOCA																				{ alloca() }	
	| CMP																					{ compare() }
	| GETIP																					{ getIp() }
	| HALT																					{ halt() }
	| CALL																					{ call() }
	| NCALL																					{ callNative() }
	| GETSP																					{ getSp() }
	| SWRITE																				{ stackWrite() }
	| SREAD																					{ stackRead() }
	| SUB																					{ sub() }
	| MUL																					{ mul() }
	| DIV																					{ div() }
	| MOD																					{ modOperation() }
	| AND																					{ bitAnd() }
	| OR																					{ bitOr() }
	| NOT																					{ bitNot() }
	| XOR																					{ xor() }
	| NOR																					{ nor() }
	| SHIFTR																				{ shiftRight() }
	| SHIFTL																				{ shiftLeft() }
	| SETIP																					{ setIp() }
	| SETSP																					{ setSp() }
	| BYTE dataList																			{ memoryByte(List.rev $2) }
	| WORD integerList																		{ memoryWord(List.rev $2) }
	| DWORD integerList																		{ memoryDword(List.rev $2) }
	| directives																			{ $1 }

directives:
	| JUMP_DIRECTIVE primaryExpression														{ jumpDirective($2) }
	| INVOKE_DIRECTIVE LPAREN argumentList RPAREN											{ invoke($1, List.rev $3) }
	| NCALL_DIRECTIVE LPAREN primaryExpression RPAREN										{ nativeInvoke($3, List.empty) }
	| NCALL_DIRECTIVE LPAREN primaryExpression COMMA argumentList RPAREN					{ nativeInvoke($3, List.rev $5) }	
	| MOV_DIRECTIVE IDENTIFIER COMMA expression												{ movDirective($2, $4) }
	| ADD_DIRECTIVE primaryExpression COMMA primaryExpression								{ addDirective($2, $4) }
	| SUB_DIRECTIVE primaryExpression COMMA primaryExpression								{ subDirective($2, $4) }
	| MUL_DIRECTIVE primaryExpression COMMA primaryExpression								{ mulDirective($2, $4) }
	| DIV_DIRECTIVE primaryExpression COMMA primaryExpression								{ divDirective($2, $4) }
	| MOD_DIRECTIVE primaryExpression COMMA primaryExpression								{ modDirective($2, $4) }
	| CMP_DIRECTIVE primaryExpression COMMA primaryExpression								{ cmpDirective($2, $4) }
	| AND_DIRECTIVE primaryExpression COMMA primaryExpression								{ andDirective($2, $4) }
	| OR_DIRECTIVE primaryExpression COMMA primaryExpression								{ orDirective($2, $4) }
	| SHIFTR_DIRECTIVE primaryExpression COMMA primaryExpression							{ shiftrDirective($2, $4) }
	| SHIFTL_DIRECTIVE primaryExpression COMMA primaryExpression							{ shiftlDirective($2, $4) }
	| XOR_DIRECTIVE primaryExpression COMMA primaryExpression								{ xorDirective($2, $4) }
	| NOR_DIRECTIVE primaryExpression COMMA primaryExpression								{ norDirective($2, $4) }
	| READB_DIRECTIVE primaryExpression														{ readByteDirective($2) }
	| READW_DIRECTIVE primaryExpression														{ readWordDirective($2) }
	| READDW_DIRECTIVE primaryExpression													{ readDoubleWordDirective($2) }
	| NREADB_DIRECTIVE primaryExpression													{ nativeReadByteDirective($2) }
	| NREADW_DIRECTIVE primaryExpression													{ nativeReadWordDirective($2) }
	| NREADDW_DIRECTIVE primaryExpression													{ nativeReadDoubleWordDirective($2) }
	| WRITEB_DIRECTIVE primaryExpression COMMA primaryExpression							{ writeByteDirective($4, $2) }
	| WRITEW_DIRECTIVE primaryExpression COMMA primaryExpression							{ writeWordDirective($4, $2) }
	| WRITEDW_DIRECTIVE primaryExpression COMMA primaryExpression							{ writeDoubleWordDirective($4, $2) }
	| NWRITEB_DIRECTIVE primaryExpression COMMA primaryExpression							{ nativeWriteByteDirective($4, $2) }
	| NWRITEW_DIRECTIVE primaryExpression COMMA primaryExpression							{ nativeWriteWordDirective($4, $2) }
	| NWRITEDW_DIRECTIVE primaryExpression COMMA primaryExpression							{ nativeWriteDoubleWordDirective($4, $2) }
	| INC_DIRECTIVE IDENTIFIER																{ incDirective($2) }

argumentList:
	|																						{ List.empty }
	| primaryExpression																		{ [$1] }
	| argumentList COMMA primaryExpression													{ $3::$1 }

primaryExpression:
	| INTEGER																				{ number($1) }
	| IDENTIFIER																			{ identifier($1) }

expression:
	| additiveExpression																	{ $1 }	

additiveExpression:
    | multiplicativeExpression																{ $1 }
    | additiveExpression PLUS_OP multiplicativeExpression									{ addDirectiveExpression($1, $3) }
    | additiveExpression MINUS_OP multiplicativeExpression									{ subDirectiveExpression($1, $3) }

multiplicativeExpression:
    | primaryExpression																		{ $1 }
    | multiplicativeExpression MUL_OP primaryExpression										{ mulDirectiveExpression($1, $3) }
    | multiplicativeExpression DIV_OP primaryExpression										{ divDirectiveExpression($1, $3) }
	| multiplicativeExpression MOD_OP primaryExpression										{ modDirectiveExpression($1, $3) }
	| LPAREN expression RPAREN																{ $2 }

integerList:
	| integerList COMMA INTEGER																{ $3::$1 }
	| INTEGER																				{ [$1] }

dataList:
	| dataList COMMA data																	{ $3::$1 }
	| data																					{ [$1] }

data:
	| INTEGER																				{ [$1] }																				
	| STRING																				{ getStringBytes($1) }
